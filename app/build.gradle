plugins {
    id 'java'
    id 'application'
}

group 'io.github.leo40git.sltbg'
version '1.0-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

jar {
    manifest {
        attributes("Main-Class": project.main_class)
    }
}

application {
    mainClass = project.main_class
}

repositories {
    mavenCentral()
    maven {
        name = "QuiltMC Maven"
        url = "https://maven.quiltmc.org/repository/release"
    }
}

sourceSets {
    dev {
        compileClasspath += sourceSets.main.runtimeClasspath
        runtimeClasspath += sourceSets.main.runtimeClasspath
    }
}

dependencies {
    compileOnly 'org.jetbrains:annotations:23.0.0'
    implementation "org.apache.logging.log4j:log4j-core:${project.log4j_version}"
    implementation "org.quiltmc:quilt-json5:${project.qjson5_version}"
    implementation "de.skuzzle:semantic-version:${project.semver_version}"

    devRuntimeOnly "org.fusesource.jansi:jansi:${project.jansi_version}"

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

processResources {
    inputs.property "version", project.version

    filesMatching("build_info.json") {
        expand "version": project.version
    }
}

test {
    useJUnitPlatform()
}
